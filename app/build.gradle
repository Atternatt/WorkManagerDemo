apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion build_versions.target_sdk

    //region default config
    defaultConfig {
        applicationId "com.m2f.app"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }
    //endregion

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        def endpoint = "ENDPOINT"
        def string = "String"
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField string, endpoint, '"http://www.google.com"'
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            buildConfigField string, endpoint, '"http://www.google.com"'
        }
    }

    //region compile option
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    //endregion

    //region conflicts
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/data_debug.kotlin_module'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage' //Some libraries have issues with this.
        disable 'OldTargetApi' //Lint gives this warning but SDK 20 would be Android L Beta.
    }

    configurations.all {
        resolutionStrategy.force 'com.squareup.okhttp3:okhttp:3.9.0'
    }
    //endregion

    //region databinding options
    dataBinding {
        enabled = true
    }
    //endregion
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //region Android Framework
    implementation deps.kotlin.stdlib
    implementation deps.support.app_compat
    implementation deps.support.core
    implementation deps.work.runtime
    implementation deps.work.runtime_ktx
    //to be considered
    //implementation deps.work.firebase
    implementation deps.lifecycle.extensions
    implementation deps.support.core_utils

    //endregion

    //region test
    testImplementation deps.junit
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.atsl.rules
    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.work.testing
    androidTestImplementation deps.arch_core.testing
    androidTestImplementation deps.mockito.android
    testImplementation deps.mockito.core
    //endregion

    //region dependency injection
    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    kapt deps.dagger.compiler
    kapt deps.dagger.android_support_compiler
    //endregion

    //region Rx
    implementation deps.rx.inject
    compileOnly deps.rx.annotations
    implementation deps.rx.rxJava2
    implementation deps.rx.rxJava2Android
    implementation deps.rx.rxJava2Kotlin
    //endregion

    //region Network
    implementation deps.retrofit.runtime
    implementation deps.retrofit.mock
    implementation deps.retrofit.gson
    implementation deps.mock_web_server
    implementation deps.retrofit.rx2
    implementation deps.stetho
    debugImplementation deps.chuck
    releaseImplementation deps.chuck_noop
    //endregion

    //region router
    implementation deps.cicerone
    //endregion

    //region utils
    implementation deps.dexter
    implementation deps.glide.runtime
    kapt deps.glide.compiler
    implementation deps.timber
    //endregion
}
